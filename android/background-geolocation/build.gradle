apply plugin: 'com.android.library'

def DEFAULT_COMPILE_SDK_VERSION     = 26
def DEFAULT_BUILD_TOOLS_VERSION     = "26.0.2"
def DEFAULT_TARGET_SDK_VERSION      = 26
def DEFAULT_PLAY_SERVICES_VERSION   = "11.8.0"
def DEFAULT_SUPPORT_LIB_VERSION     = "26.1.0"

android {
    compileSdkVersion rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
    buildToolsVersion rootProject.hasProperty('buildToolsVersion') ? rootProject.buildToolsVersion : DEFAULT_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion rootProject.hasProperty('targetSdkVersion') ? rootProject.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories{
    jcenter()
    maven {
        url 'https://maven.google.com'
    }
    maven {
        url './libs'
    }
}


dependencies {
    def playServicesVersion = DEFAULT_PLAY_SERVICES_VERSION
    if (rootProject.hasProperty('playServicesVersion')) {
        playServicesVersion = project.playServicesVersion
    } else if (rootProject.hasProperty('googlePlayServicesVersion')) {
        playServicesVersion = project.googlePlayServicesVersion
    }
    def supportLibVersion = rootProject.hasProperty('supportLibVersion') ? rootProject.supportLibVersion : DEFAULT_SUPPORT_LIB_VERSION

    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.google.android.gms:play-services-location:$playServicesVersion"
    api 'org.greenrobot:eventbus:3.0.0'
    api 'com.squareup.okhttp3:okhttp:3.8.1'
    api(group: 'com.transistorsoft', name:'tslocationmanager', version: '+')
    api 'org.slf4j:slf4j-api:1.7.21'
    api 'com.github.tony19:logback-android-core:1.1.1-6'
    api('com.github.tony19:logback-android-classic:1.1.1-6') {
        exclude group: 'com.google.android', module: 'android'
    }
    // android-permissions
    api 'com.intentfilter:android-permissions:0.1.5'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}
